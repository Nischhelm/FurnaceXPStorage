plugins {
    id 'eclipse'
    id 'idea'
    id 'net.minecraftforge.gradle' version "${forge_gradle_version}"
    id 'org.spongepowered.mixin' version "${mixin_gradle_version}"
    id 'wtf.gofancy.fancygradle' version "${fancy_gradle_version}"
}

version = mod_version
group = mod_id
base.archivesName = mod_name

java.toolchain.languageVersion = JavaLanguageVersion.of(8)

minecraft {
    mappings channel: mappings_channel, version: mappings_version

    runs {
        client {
            args "--username=InDev"

            jvmArg "-Dfml.coreMods.load=${mixin_loader_class}"
            jvmArg "-Dmixin.hotSwap=true"
            jvmArg "-Dmixin.checks.interfaces=true"

            workingDirectory project.file('run')

            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'
        }

        server {

            jvmArg "-Dfml.coreMods.load=${mixin_loader_class}"
            jvmArg "-Dmixin.hotSwap=true"
            jvmArg "-Dmixin.checks.interfaces=true"

            workingDirectory project.file('run')

            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'
        }
    }
}

repositories {
    flatDir { dirs 'libs' }
    maven { url = 'https://www.cursemaven.com' }
    maven { url = 'https://maven.blamejared.com' }
    maven { url = "https://api.modrinth.com/maven" }
}

dependencies {
    minecraft "net.minecraftforge:forge:${minecraft_version}-${forge_version}"

    compileOnly "org.spongepowered:mixin:${mixin_version}"
    annotationProcessor "org.spongepowered:mixin:${mixin_version}:processor"
    compileOnly "io.github.llamalad7:mixinextras-common:${mixin_extras_version}"
    annotationProcessor "io.github.llamalad7:mixinextras-common:${mixin_extras_version}"

    implementation fg.deobf("curse.maven:fermiumbooter-${fermiumbooter_version}")

    compileOnly fg.deobf("curse.maven:nethercraft-classic-${nethercraft_version}")
    compileOnly fg.deobf("curse.maven:betternether-${betternether_version}")

    compileOnly fg.deobf("libs:BetterEndForge-Unofficial-GBPort:${betterend_version}") //unreleased
    compileOnly fg.deobf("curse.maven:fastfurnace-${fastfurnace_version}")
    compileOnly fg.deobf("curse.maven:ironfurnaces-${ironfurnaces_version}")
    compileOnly fg.deobf("curse.maven:bwm-suite-${bwm_version}")
    compileOnly fg.deobf("curse.maven:morefurnaces-${morefurnaces_version}")
    compileOnly fg.deobf("curse.maven:morefurnaces-${morefurnacesnomifactory_version}")
    compileOnly fg.deobf("curse.maven:cxlibrary-${cxlibrary_version}")
    compileOnly fg.deobf("curse.maven:better-furnaces-reforged-${betterfurnacesreforged_version}")
    compileOnly fg.deobf("curse.maven:furnace-overhaul-${furnaceoverhaul_version}")
    compileOnly fg.deobf("curse.maven:furnus-${furnus_version}")
    compileOnly fg.deobf("curse.maven:limelib-${limelib_version}")
    compileOnly fg.deobf("curse.maven:mysticalagriculture-${mysticalagriculture_version}")
    compileOnly fg.deobf("curse.maven:nuclearcraft-mod-${nuclearcraft_version}")
    implementation fg.deobf("curse.maven:official-divinerpg-363543:3081433")
    implementation fg.deobf("curse.maven:simplecoreapi-226136:2629840")
    implementation fg.deobf("curse.maven:natura-74120:2711439")

    compileOnly fg.deobf("curse.maven:jei-${jei_version}")

    compileOnly fg.deobf("CraftTweaker2:CraftTweaker2-${crafttweaker_version}")
    compileOnly fg.deobf("CraftTweaker2:ZenScript:${zs_version}")
    compileOnly fg.deobf("CraftTweaker2:CraftTweaker2-API:${zs_version}")
}

mixin {
    add sourceSets.main, "mixins.${mod_id}.refmap.json"
}

fancyGradle {
    patches {
        resources
        coremods
        asm
        mergetool
    }
}

processResources {
    var replaceProperties = [
            'mod_id': mod_id,
            'mod_name': mod_name,
            'version': mod_version,
            'mcversion': minecraft_version,
            'mod_description': mod_description,
            'mod_author': mod_author
    ]

    inputs.properties replaceProperties

    filesMatching('mcmod.info') {
        expand replaceProperties
    }
}

jar {
    manifest {
        def attr = [:]
        attr['Specification-Title']         = mod_name
        attr['Specification-Vendor']        = mod_author
        attr['Specification-Version']       = '1'
        attr['Implementation-Title']        = mod_name
        attr['Implementation-Vendor']       = mod_author
        attr['Implementation-Version']      = mod_version
        attr['Implementation-Timestamp']    = new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        attr['FMLCorePlugin']               = mixin_loader_class
        attr['FMLCorePluginContainsFMLMod'] = 'true'
        attr['TweakClass']                  = 'org.spongepowered.asm.launch.MixinTweaker'
        attr['ForceLoadAsMod'] = "true"

        def currentTasks = gradle.startParameter.taskNames
        if (currentTasks[0] == 'build' || currentTasks[0] == 'assemble')
            attr['ForceLoadAsMod'] = "true"
        attributes(attr)
    }
}

tasks.register("copyReobfJar", Copy) {
    dependsOn("reobfJar")

    from(layout.buildDirectory.file("reobfJar/output.jar"))
    into(layout.projectDirectory.dir("run/mods"))
    rename("output.jar", "${mod_name}-${mod_version}.jar")
}

afterEvaluate {
    tasks.matching { it.name.startsWith("reobfJar") }.configureEach {
        finalizedBy("copyReobfJar")
    }
}

jar.finalizedBy('reobfJar')